open Base

type t =
  | RespList of t list
  | BulkString of string
  | SimpleString of string
  | Integer of int
  | Null
  | NullArray
  | RespBinary of string
  | RespConcat of t list
  | RespError of string
[@@deriving compare, equal, sexp]

exception InvalidData

let null_string = "$-1\r\n"
let integer_regex = Str.regexp "^\\:\\([\\+\\-]?\\)\\(.*\\)\r\n"
let simple_string_regex = Str.regexp "^\\+\\(.*\\)\r\n"
let bulk_string_regex = Str.regexp "^\\$\\([0-9]+\\)\r\n\\(.*\\)\r\n"
let list_regexp = Str.regexp "^\\*\\([0-9]+\\)\r\n"

let rec from_string_internal (str : string) (pos : int) : int * t =
  let list_result = Str.string_match list_regexp str pos in
  if list_result then (
    let prefix_length = String.length (Str.matched_group 0 str) in
    let count = Int.of_string (Str.matched_group 1 str) in
    let list = ref [] in
    let total_length = ref 0 in
    for _idx = 0 to count - 1 do
      let length, item =
        from_string_internal str (pos + prefix_length + !total_length)
      in
      total_length := !total_length + length;
      list := item :: !list
    done;
    (!total_length + prefix_length, RespList (List.rev !list)))
  else
    let integer_result = Str.string_match integer_regex str pos in
    if integer_result then
      let prefix = Str.matched_group 1 str in
      let factor = match prefix with "-" -> -1 | _ -> 1 in
      let value = Int.of_string (Str.matched_group 2 str) in
      (String.length (Str.matched_group 0 str), Integer (value * factor))
    else
      let simple_string_result = Str.string_match simple_string_regex str pos in
      if simple_string_result then
        ( String.length (Str.matched_group 0 str),
          SimpleString (Str.matched_group 1 str) )
      else
        let string_result = Str.string_match bulk_string_regex str pos in
        if string_result then
          ( String.length (Str.matched_group 0 str),
            BulkString (Str.matched_group 2 str) )
        else (0, BulkString "")

let from_string (str : string) (pos : int) : t =
  let _, item = from_string_internal str pos in
  item

let rec to_string (item : t) : string =
  match item with
  | Integer integer_value ->
      Printf.sprintf ":%s%d\r\n"
        (if integer_value < 0 then "-" else "")
        integer_value
  | SimpleString str -> Printf.sprintf "+%s\r\n" str
  | BulkString str -> Printf.sprintf "$%d\r\n%s\r\n" (String.length str) str
  | RespList list ->
      Printf.sprintf "*%d\r\n%s" (List.length list)
        (String.concat ~sep:"" (List.map ~f:(fun x -> to_string x) list))
  | Null -> null_string
  | NullArray -> "*-1\r\n"
  | RespBinary str -> Printf.sprintf "$%d\r\n%s" (String.length str) str
  | RespConcat list ->
      String.concat ~sep:"" (List.map ~f:(fun x -> to_string x) list)
  | RespError error -> Printf.sprintf "-%s\r\n" error

let to_sexp (input : t) : Sexp.t = sexp_of_t input
let to_simple_string (str : string) : string = SimpleString str |> to_string
let to_bulk_string (str : string) : string = BulkString str |> to_string
let to_integer_string (value : int) : string = Integer value |> to_string

let remove_trailing_cr value =
  String.sub ~pos:0 ~len:(String.length value - 1) value

let read_line_int (channel : Stdlib.in_channel) : int =
  let str = Stdlib.input_line channel in
  Int.of_string (remove_trailing_cr str)

let read_binary_from_channel (channel : Stdlib.in_channel) : t =
  match Stdlib.input_char channel with
  | '$' ->
      let count = read_line_int channel in
      let str = Stdlib.really_input_string channel count in
      RespBinary str
  | _ -> raise InvalidData

let rec read_from_channel (channel : Stdlib.in_channel) : t =
  match Stdlib.input_char channel with
  | ':' -> read_line_int channel |> fun value -> Integer value
  | '+' ->
      Stdlib.input_line channel |> remove_trailing_cr |> fun value ->
      SimpleString value
  | '-' ->
      Stdlib.input_line channel |> remove_trailing_cr |> fun value ->
      RespError value
  | '$' ->
      let count = read_line_int channel in
      let str = Stdlib.really_input_string channel count in
      ignore @@ Stdlib.really_input_string channel 2;
      (* remove \r\n from tail *)
      BulkString str
  | '*' ->
      let count = read_line_int channel in
      List.range 0 count |> List.map ~f:(fun _ -> read_from_channel channel)
      |> fun l -> RespList l
  | _ -> RespError "Error: not implemented"

let%test_unit "from_string integer" =
  [%test_eq: t] (from_string ":+55\r\n" 0) (Integer 55)

let%test_unit "from_string integer" =
  [%test_eq: t] (from_string ":55\r\n" 0) (Integer 55)

let%test_unit "from_string integer" =
  [%test_eq: t] (from_string ":-55\r\n" 0) (Integer (55 * -1))

let%test_unit "from_string simple string" =
  [%test_eq: t] (from_string "+Hey\r\n" 0) (SimpleString "Hey")

let%test_unit "from_string bulk string" =
  [%test_eq: t] (from_string "$3\r\nHey\r\n" 0) (BulkString "Hey")

let%test_unit "to_string bulk string" =
  [%test_eq: string] (to_string (BulkString "Hey")) "$3\r\nHey\r\n"

let%test_unit "to_string binary" =
  [%test_eq: string] (to_string (RespBinary "Hey")) "$3\r\nHey"

let%test_unit "to_string concat" =
  [%test_eq: string]
    (to_string (RespConcat [ BulkString "Hey"; RespBinary "xxx" ]))
    "$3\r\nHey\r\n$3\r\nxxx"

let%test_unit "to_string simple string" =
  [%test_eq: string] (to_string (SimpleString "Hey")) "+Hey\r\n"

let%test_unit "to_string integer" =
  [%test_eq: string] (to_string (Integer 55)) ":55\r\n"

let%test_unit "to_string error" =
  [%test_eq: string] (to_string (RespError "Error 77")) "-Error 77\r\n"

let%test_unit "from_string list" =
  [%test_eq: t]
    (from_string "*2\r\n$5\r\nhello\r\n$5\r\nworld\r\n" 0)
    (RespList [ BulkString "hello"; BulkString "world" ])

let%test_unit "from_string list with 2 bulk strings" =
  [%test_eq: string]
    (to_string (RespList [ BulkString "hello"; BulkString "world" ]))
    "*2\r\n$5\r\nhello\r\n$5\r\nworld\r\n"
