open Base

type command_t = string * string list
type command_queue_t = command_t Queue.t

type post_process_t =
  | RegisterSlave of Resp.t option
  | Mutation of Resp.t
  | Propagate of Resp.t
  | Noop

exception InvalidData

module StringSet = Stdlib.Set.Make (String)

type context_t = {
  role : Options.role_t;
  socket : Unix.file_descr;
  command_queue : command_queue_t option;
  post_process : post_process_t;
  subscription_mode : bool;
  slave : Master.slave_t option;
}

let parse_command_line (command : Resp.t) : string * string list =
  let parse_arg arg =
    match arg with
    | Resp.Integer integer -> Int.to_string integer
    | Resp.BulkString str -> str
    | Resp.SimpleString str -> str
    | Resp.RespList _ -> ""
    | Resp.NullArray -> ""
    | Resp.Null -> ""
    | Resp.RespBinary _str -> ""
    | Resp.RespConcat _l -> ""
    | Resp.RespError error -> error
    | Resp.RespIgnore -> ""
  in
  match command with
  | Resp.RespList (Resp.BulkString command :: rest) ->
      (String.lowercase command, List.map ~f:parse_arg rest)
  | _ -> raise InvalidData

let resp_from_command ((command, rest) : command_t) =
  let list =
    Resp.BulkString command :: List.map rest ~f:(fun a -> Resp.BulkString a)
  in
  Resp.RespList list

let process_ping () : Resp.t = Resp.SimpleString "PONG"

let store_to_list (storage_value : Store.t option) : Lists.t option =
  match storage_value with Some (StorageList l) -> Some l | _ -> None

let store_to_stream (storage_value : Store.t option) : Streams.t option =
  match storage_value with
  | Some (StorageStream stream) -> Some stream
  | _ -> None

let store_to_int (storage_value : Store.t option) : Ints.t option =
  match storage_value with Some (StorageInt i) -> Some i | _ -> None

let store_to_string (storage_value : Store.t option) : Strings.t option =
  match storage_value with Some (StorageString str) -> Some str | _ -> None

let store_to_sortedset (storage_value : Store.t option) : Sortedsets.t option =
  match storage_value with Some (StorageSortedSet set) -> Some set | _ -> None

let sortedset_to_store (set : Sortedsets.t option) : Store.t option =
  match set with Some set -> Some (Store.StorageSortedSet set) | _ -> None

let stream_to_store (stream : Streams.t option) : Store.t option =
  match stream with
  | Some stream -> Some (Store.StorageStream stream)
  | _ -> None

let list_to_store (l : Lists.t option) : Store.t option =
  match l with Some l -> Some (Store.StorageList l) | None -> None

let int_to_store (value : Ints.t option) : Store.t option =
  match value with Some i -> Some (Store.StorageInt i) | None -> None

let string_to_store (str : Strings.t option) : Store.t option =
  match str with Some str -> Some (Store.StorageString str) | None -> None

let resp_from_store (item : Store.t) : Resp.t =
  match item with
  | Store.StorageInt i -> Resp.BulkString (Ints.to_int i |> Int.to_string)
  | Store.StorageString str -> Strings.to_resp str
  | Store.StorageList l -> Lists.to_resp l
  | Store.StorageStream _ -> Resp.SimpleString "OK"
  | Store.StorageSortedSet _ -> Resp.SimpleString "OK"

let get (key : string) : Resp.t =
  let value = Store.get key in
  match value with None -> Resp.Null | Some v -> resp_from_store v

let set ~(expiry : (string * string) option) (key : string) (value : string) :
    Resp.t =
  let lifetime =
    match expiry with
    | Some (expiry_type, expiry_value) ->
        Lifetime.create_expiry expiry_type expiry_value
    | None -> Lifetime.Forever
  in
  match Int.of_string_opt value with
  | Some value ->
      Store.mutate key lifetime store_to_int int_to_store (Ints.set value)
  | None ->
      Store.mutate key lifetime store_to_string string_to_store
        (Strings.set value)

let incr (key : string) : Resp.t =
  let valid =
    match Store.get key with
    | None -> true
    | Some (StorageInt _) -> true
    | _ -> false
  in
  match valid with
  | true ->
      Store.mutate key Lifetime.Forever store_to_int int_to_store Ints.incr
  | false -> Resp.RespError "ERR value is not an integer or out of range"

let rpush (key : string) (rest : string list) : Resp.t =
  Store.mutate key Lifetime.Forever store_to_list list_to_store
    (Lists.rpush rest)

let lpush (key : string) (rest : string list) =
  Store.mutate key Lifetime.Forever store_to_list list_to_store
    (Lists.lpush rest)

let lrange (key : string) (from_idx : string) (to_idx : string) : Resp.t =
  Store.query key store_to_list (Lists.lrange from_idx to_idx)

let llen (key : string) : Resp.t = Store.query key store_to_list Lists.llen

let lpop (key : string) (count : int) : Resp.t =
  Store.mutate key Lifetime.Forever store_to_list list_to_store
    (Lists.lpop count)

let blpop (key : string) (timeout : string) : Resp.t =
  let timeout =
    match Float.of_string_opt timeout with
    | Some v -> v
    | None -> Int.of_string timeout |> Float.of_int
  in
  Store.pop_list_or_wait key timeout (Lists.lpop 1)

let type_cmd (key : string) : Resp.t =
  (match Store.get key with
    | None -> "none"
    | Some v -> (
        match v with
        | Store.StorageInt _ -> "integer"
        | Store.StorageList _ -> "list"
        | Store.StorageString _ -> "string"
        | Store.StorageStream _ -> "stream"
        | Store.StorageSortedSet _ -> "sortedset"))
  |> fun v -> Resp.SimpleString v

let echo (message : string) : Resp.t = Resp.BulkString message

let xadd (key : string) (id : string) (rest : string list) : Resp.t =
  Store.mutate key Lifetime.Forever store_to_stream stream_to_store
    (Streams.xadd id rest)

let xrange (key : string) (from_id : string) (to_id : string) : Resp.t =
  Store.query key store_to_stream (Streams.xrange from_id to_id)

let xread (rest : string list) (timeout : Lifetime.t option) : Resp.t =
  let count = List.length rest / 2 in
  let keys = List.take rest count in
  let from_ids = List.sub rest ~pos:count ~len:(List.length rest - count) in
  List.zip_exn keys from_ids
  |> List.map ~f:(fun (key, from_id) ->
      Store.query key store_to_stream (Streams.xread key from_id timeout))
  |> fun l ->
  match l with
  | [ Resp.NullArray ] | [] -> Resp.NullArray
  | _ as l -> Resp.RespList l

let info (args : string list) : Resp.t =
  let options = Options.parse_options (Sys.get_argv ()) in
  match args with
  | [ "replication" ] -> (
      match options.role with
      | Master ->
          Resp.BulkString
            "role:master\r\n\
             master_repl_offset:0\r\n\
             master_replid:8371b4fb1155b71f4a04d3e1bc3e18c4a990aeeb\r\n"
      | Slave _ -> Resp.BulkString "role:slave")
  | _ -> Resp.RespError "ERR Unknown role"

let config_get (name : string) : Resp.t =
  let options = Options.parse_options (Sys.get_argv ()) in
  match options.rdb with
  | None -> Resp.RespError "ERR: No config to read"
  | Some rdb ->
      RespList
        [
          Resp.BulkString name;
          Resp.BulkString
            (match name with
            | "dir" -> rdb.dir
            | "dbfilename" -> rdb.filename
            | _ -> "");
        ]

let multi (context : context_t) : Resp.t * context_t =
  let context = { context with command_queue = Some (Queue.create ()) } in
  (Resp.SimpleString "OK", context)

let replconf (context : context_t) (args : string list) : Resp.t =
  Stdlib.Printf.printf "MASTER: replconf %s\n" (String.concat ~sep:"," args);
  Stdlib.flush Stdlib.stdout;
  match context.slave with
  | Some slave -> (
      match args with
      | [ "ACK"; bytes_ack ] ->
          Master.process_replconf_ack slave (Int.of_string bytes_ack);
          Resp.RespIgnore
      | _ -> Resp.SimpleString "OK")
  | None -> Resp.SimpleString "OK"

let wait (required_slaves : string) (timeout_ms : string) : Resp.t =
  let required_slaves = Int.of_string required_slaves in
  let lifetime =
    Lifetime.create_expiry_with_ms (Int64.of_string timeout_ms)
    |> Lifetime.to_absolute_expires
  in
  Resp.Integer (Master.sync_slaves_for_listener required_slaves lifetime)

let psync (context : context_t) (_args : string list) : Resp.t * context_t =
  let decoded_rdb_result =
    Base64.decode
      "UkVESVMwMDEx+glyZWRpcy12ZXIFNy4yLjD6CnJlZGlzLWJpdHPAQPoFY3RpbWXCbQi8ZfoIdXNlZC1tZW3CsMQQAPoIYW9mLWJhc2XAAP/wbjv+wP9aog=="
  in
  let result, rdb =
    match decoded_rdb_result with
    | Ok empty_rdb ->
        ( Resp.SimpleString
            "FULLRESYNC 8371b4fb1155b71f4a04d3e1bc3e18c4a990aeeb 0",
          Some (Resp.RespBinary empty_rdb) )
    | Error _ -> (Resp.RespError "ERR Decoding empty RDB file", None)
  in
  (result, { context with post_process = RegisterSlave rdb })

let keys (pattern : string) : Resp.t =
  Store.keys pattern |> List.map ~f:(fun k -> Resp.BulkString k) |> fun l ->
  Resp.RespList l

let subscribe (context : context_t) (channel_name : string) : Resp.t * context_t
    =
  let thread_id = Thread.id @@ Thread.self () in
  Pubsub.subscribe channel_name thread_id context.socket;
  let channels = Pubsub.channels_for_subscriber thread_id in
  let result =
    Resp.RespList
      [
        Resp.BulkString "subscribe";
        Resp.BulkString channel_name;
        Resp.Integer (List.length channels);
      ]
  in
  (result, { context with subscription_mode = true })

let publish (channel_name : string) (message : string) : Resp.t =
  Resp.Integer (Pubsub.publish channel_name message)

let unsubscribe (context : context_t) (channel_name : string) :
    Resp.t * context_t =
  let thread_id = Thread.id @@ Thread.self () in
  Pubsub.unsubscribe channel_name thread_id;
  let channels = Pubsub.channels_for_subscriber thread_id in
  let result =
    Resp.RespList
      [
        Resp.BulkString "unsubscribe";
        Resp.BulkString channel_name;
        Resp.Integer (List.length channels);
      ]
  in
  (result, context)

let zadd (key : string) (score : string) (member : string) : Resp.t =
  Store.mutate key Lifetime.Forever store_to_sortedset sortedset_to_store
  @@ Sortedsets.zadd ~member ~score:(Float.of_string score)

let zrank (key : string) (value : string) : Resp.t =
  Store.query key store_to_sortedset @@ Sortedsets.zrank ~value

let zcard (key : string) : Resp.t =
  Store.query key store_to_sortedset @@ Sortedsets.zcard

let zscore (key : string) (member : string) : Resp.t =
  Store.query key store_to_sortedset @@ Sortedsets.zscore member

let zrem (key : string) (member : string) : Resp.t =
  Store.mutate key Lifetime.Forever store_to_sortedset sortedset_to_store
  @@ Sortedsets.zrem member

let zrange (key : string) (from_index : string) (to_index : string) : Resp.t =
  Store.query key store_to_sortedset
  @@ Sortedsets.zrange ~from_idx:(Int.of_string from_index)
       ~to_idx:(Int.of_string to_index)

let readonly_command (context : context_t) (result : Resp.t) :
    Resp.t * context_t =
  (result, context)

let _readonly_command_with_propagation (context : context_t)
    (command : command_t) (result : Resp.t) : Resp.t * context_t =
  (result, { context with post_process = Propagate (resp_from_command command) })

let readwrite_command (context : context_t) (command : command_t)
    (result : Resp.t) : Resp.t * context_t =
  (result, { context with post_process = Mutation (resp_from_command command) })

let process_command (context : context_t) (command : command_t) :
    Resp.t * context_t =
  match command with
  | "ping", [] -> readonly_command context @@ process_ping ()
  | "set", [ key; value ] ->
      readwrite_command context command @@ set key value ~expiry:None
  | "set", [ key; value; expiry_type; expiry_value ] ->
      readwrite_command context command
      @@ set ~expiry:(Some (expiry_type, expiry_value)) key value
  | "incr", [ key ] -> readwrite_command context command @@ incr key
  | "get", [ key ] -> readonly_command context @@ get key
  | "rpush", key :: rest -> readwrite_command context command @@ rpush key rest
  | "lpush", key :: rest -> readwrite_command context command @@ lpush key rest
  | "lrange", [ key; from_idx; to_idx ] ->
      readonly_command context @@ lrange key from_idx to_idx
  | "llen", [ key ] -> readonly_command context @@ llen key
  | "lpop", [ key ] -> readonly_command context @@ lpop key 1
  | "lpop", [ key; count ] ->
      readonly_command context @@ lpop key (Int.of_string count)
  | "blpop", [ key; timeout ] ->
      readwrite_command context command @@ blpop key timeout
  | "type", [ key ] -> readonly_command context @@ type_cmd key
  | "echo", [ message ] -> readonly_command context @@ echo message
  | "xadd", key :: id :: rest ->
      readwrite_command context command @@ xadd key id rest
  | "xrange", [ key; from_id; to_id ] ->
      readonly_command context @@ xrange key from_id to_id
  | "xread", "block" :: timeout :: "streams" :: rest ->
      readonly_command context
      @@ xread rest (Some (Lifetime.create_expiry "px" timeout))
  | "xread", _ :: rest -> readonly_command context @@ xread rest None
  | "exec", [] -> (Resp.RespError "ERR EXEC without MULTI", context)
  | "discard", [] -> (Resp.RespError "ERR DISCARD without MULTI", context)
  | "replconf", rest -> readonly_command context @@ replconf context rest
  | "psync", rest -> psync context rest
  | "info", rest -> readonly_command context @@ info rest
  | "wait", [ num_replicas; timeout_ms ] ->
      readonly_command context @@ wait num_replicas timeout_ms
  | "config", [ "GET"; name ] -> readonly_command context @@ config_get name
  | "subscribe", [ channel_name ] -> subscribe context channel_name
  | "publish", [ channel_name; value ] ->
      readonly_command context @@ publish channel_name value
  | "keys", [ pattern ] -> readonly_command context @@ keys pattern
  | "zadd", [ key; score; value ] ->
      readwrite_command context command @@ zadd key score value
  | "zrank", [ key; value ] -> readonly_command context @@ zrank key value
  | "zrange", [ key; from_index; to_index ] ->
      readonly_command context @@ zrange key from_index to_index
  | "zcard", [ key ] -> readonly_command context @@ zcard key
  | "zscore", [ key; member ] -> readonly_command context @@ zscore key member
  | "zrem", [ key; member ] ->
      readwrite_command context command @@ zrem key member
  | _ -> (Resp.Null, context)

let exec (context : context_t) : Resp.t * context_t =
  match context.command_queue with
  | None ->
      ( Resp.RespError "ERR EXEC without MULTI",
        { context with command_queue = None } )
  | Some queue ->
      Queue.to_list queue
      |> List.map ~f:(fun command ->
          let result, _ = process_command context command in
          result)
      |> fun list_of_resp ->
      (Resp.RespList list_of_resp, { context with command_queue = None })

let process_transaction_command (context : context_t)
    (queue : command_t Queue.t) (command : command_t) : Resp.t * context_t =
  match command with
  | "exec", [] -> exec context
  | "multi", _ ->
      (Resp.RespError "ERR: nested transactions not supported", context)
  | "discard", _ ->
      (Resp.SimpleString "OK", { context with command_queue = None })
  | _ ->
      Queue.enqueue queue command;
      (Resp.SimpleString "QUEUED", context)

let process_subscription_command (context : context_t) (command : command_t) :
    Resp.t * context_t =
  match command with
  | "subscribe", [ channel_name ] -> subscribe context channel_name
  | "unsubscribe", [ channel_name ] -> unsubscribe context channel_name
  | "psubscribe", [ channel_name ] -> subscribe context channel_name
  | "punsubscribe", [ channel_name ] -> unsubscribe context channel_name
  | "ping", _ ->
      (Resp.RespList [ Resp.BulkString "pong"; Resp.BulkString "" ], context)
  | "quit", _ -> subscribe context "quit"
  | cmd, _ ->
      ( Resp.RespError
          (Stdlib.Printf.sprintf
             "ERR Can't execute '%s': only (P|S)SUBSCRIBE / (P|S)UNSUBSCRIBE / \
              PING / QUIT / RESET are allowed in this context"
             cmd),
        context )

let process (context : context_t) (command : command_t) : Resp.t * context_t =
  match context.command_queue with
  | Some queue -> process_transaction_command context queue command
  | None ->
      if not context.subscription_mode then
        match command with
        | "multi", [] -> multi context
        | _ -> process_command context command
      else process_subscription_command context command
