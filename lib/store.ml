open Base
module StringMap = Stdlib.Map.Make (String)

type storage_t =
  | StorageInt of Ints.t
  | StorageString of Strings.t
  | StorageList of Lists.t
  | StorageStream of Streams.t
  | StorageSortedSet of Sortedsets.t

module RepoDef = struct
  type t = (storage_t * Lifetime.t) StringMap.t

  let repo : t ref = ref StringMap.empty
  let get () = !repo
  let set r = repo := r
end

type t = storage_t

module Repo = Protected.Resource.Make (RepoDef)

(* Listener type to be notified when an element becomes available *)
type listener = {
  lock : Stdlib.Mutex.t;
  condition : Stdlib.Condition.t;
  lifetime : Lifetime.t;
  mutable result : Resp.t option;
}

module Listeners = Protected.Resource.Make (struct
  type t = listener Queue.t StringMap.t

  let listeners : t ref = ref StringMap.empty
  let get () = !listeners
  let set l = listeners := l
end)

type wait_result =
  | WaitResult of listener * (Resp.t -> Resp.t)
  | ValueResult of Resp.t

let queue_listener (key : string) (timeout : Lifetime.t) : listener =
  let condition = Stdlib.Condition.create () in
  let lock = Stdlib.Mutex.create () in
  let lifetime = Lifetime.to_absolute_expires timeout in
  let listener = { lock; condition; lifetime; result = None } in
  Stdlib.Mutex.lock lock;
  Listeners.mutate_with_result (fun listeners ->
      let listeners, queue =
        match StringMap.find_opt key listeners with
        | None ->
            let q : listener Queue.t = Queue.create () in
            (StringMap.add key q listeners, q)
        | Some q -> (listeners, q)
      in
      Queue.enqueue queue listener;
      (listeners, listener))

let wait_result (outcome : wait_result) =
  match outcome with
  | ValueResult v -> v
  | WaitResult (listener, convert) ->
      Stdlib.Condition.wait listener.condition listener.lock;
      Listeners.query (fun _ ->
          match listener.result with
          | Some resp -> convert resp
          | None -> Resp.NullArray)

let set_no_lock (key : string) (value : t) (expires : Lifetime.t)
    (repo : RepoDef.t) : RepoDef.t =
  StringMap.add key (value, expires) repo

let notify_listeners (listener_queue : listener Queue.t)
    (resp_items : Resp.t list) : unit =
  resp_items
  |> List.iter ~f:(fun value ->
      match Queue.dequeue listener_queue with
      | None -> ()
      | Some listener ->
          listener.result <- Some value;
          Stdlib.Condition.signal listener.condition)

let get_no_lock (key : string) (repo : RepoDef.t) : t option =
  match StringMap.find_opt key repo with
  | None -> None
  | Some (value, expiry) -> (
      match expiry with
      | Forever -> Some value
      | Expires e -> if Int64.(e >= Lifetime.now ()) then Some value else None)

let set (key : string) (value : t) (expires : Lifetime.t) : unit =
  Repo.mutate (fun repo -> set_no_lock key value expires repo)

let get (key : string) : t option =
  Repo.query (fun repo -> get_no_lock key repo)

let convert_to_list (storage_value : t option) : Lists.t option =
  match storage_value with Some (StorageList l) -> Some l | _ -> None

let pop_list_or_wait (key : string) (timeout : float)
    (fn : int -> Lists.t option -> Lists.t Storeop.mutation_result) : Resp.t =
  let outcome =
    Repo.mutate_with_result (fun repo ->
        let exiting_list = get_no_lock key repo |> convert_to_list in
        let result = fn 0 exiting_list in
        match result.store with
        | Some l ->
            ( set_no_lock key (StorageList l) Lifetime.Forever repo,
              ValueResult (Resp.RespList [ Resp.BulkString key; result.return ])
            )
        | _ ->
            let lifetime =
              match timeout with
              | 0.0 -> Lifetime.Forever
              | _ -> Lifetime.create_expiry_with_s timeout
            in
            let listener = queue_listener key lifetime in
            ( repo,
              WaitResult
                ( listener,
                  fun resp -> Resp.RespList [ Resp.BulkString key; resp ] ) ))
  in
  wait_result outcome

let query (key : string) (convert : t option -> 'a option)
    (query : 'a option -> Storeop.query_result) : Resp.t =
  let outcome =
    Repo.query (fun repo ->
        match get_no_lock key repo |> convert |> query with
        | Value resp -> ValueResult resp
        | Wait (timeout, convert) ->
            let listener = queue_listener key timeout in
            WaitResult (listener, convert))
  in
  wait_result outcome

let keys (_pattern : string) : string list =
  Repo.query (fun repo ->
      StringMap.to_list repo |> List.map ~f:(fun (key, _) -> key))

let mutate (key : string) (lifetime : Lifetime.t)
    (from_store : 't option -> 'a option) (to_store : 'a option -> t option)
    (fn : int -> 'a option -> 'a Storeop.mutation_result) : Resp.t =
  Repo.mutate_with_result (fun repo ->
      let listener_queue = StringMap.find_opt key (Listeners.get ()) in
      let queue_length =
        match listener_queue with Some q -> Queue.length q | None -> 0
      in
      let result = get_no_lock key repo |> from_store |> fn queue_length in
      let modified_repo =
        match to_store result.store with
        | Some store_data ->
            (match listener_queue with
            | Some lq -> notify_listeners lq result.notify_with
            | None -> ());
            set_no_lock key store_data
              (Lifetime.to_absolute_expires lifetime)
              repo
        | None -> repo
      in
      (modified_repo, result.return))

let rec remove_expired_entries_loop () : unit =
  Repo.mutate (fun repo ->
      let current_time = Lifetime.now () in
      StringMap.filter
        (fun _ value ->
          match value with
          | _, Lifetime.Forever -> true
          | _, Lifetime.Expires e -> Int64.(e >= current_time))
        repo);
  Thread.delay 10.0;
  remove_expired_entries_loop ()

let rec expire_listeners () : unit =
  ignore
  @@ Listeners.mutate (fun listeners ->
      let current_time = Lifetime.now () in
      Stdlib.print_int (List.length (StringMap.to_list listeners));
      StringMap.map
        (fun queue ->
          Queue.filter queue ~f:(fun listener ->
              Lifetime.has_expired current_time listener.lifetime)
          |> Queue.iter ~f:(fun listener ->
              Stdlib.Condition.signal listener.condition);
          Queue.filter queue ~f:(fun listener ->
              not (Lifetime.has_expired current_time listener.lifetime)))
        listeners);
  Thread.delay 0.1;
  expire_listeners ()

let start_gc () : Thread.t = Thread.create remove_expired_entries_loop ()
let start_expire_listeners () : Thread.t = Thread.create expire_listeners ()
