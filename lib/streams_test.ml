open! Base

let create_sample_stream () =
  Some
    (Streams.create
       [
         ("0-1", [ ("hello", "world") ]);
         ("0-2", [ ("bar", "baz") ]);
         ("1-1", [ ("xx", "fff") ]);
       ])

let value_of_result (result : Storeop.query_result) : Resp.t =
  match result with Value v -> v | Wait _ -> assert false

let wait_of_result (result : Storeop.query_result) : Lifetime.t =
  match result with Value _ -> assert false | Wait (timeout, _) -> timeout

let%expect_test "xrange all data" =
  let result =
    Streams.xrange "-" "+" (create_sample_stream ()) |> value_of_result
  in
  Stdio.print_endline (Resp.to_sexp result |> Sexp.to_string_hum);
  [%expect
    {|
    (RespList
     ((RespList
       ((BulkString 0-1) (RespList ((BulkString hello) (BulkString world)))))
      (RespList
       ((BulkString 0-2) (RespList ((BulkString bar) (BulkString baz)))))
      (RespList ((BulkString 1-1) (RespList ((BulkString xx) (BulkString fff)))))))
    |}]

let%expect_test "xrange from id" =
  let result =
    Streams.xrange "0-2" "+" (create_sample_stream ()) |> value_of_result
  in
  Stdio.print_endline (Resp.to_sexp result |> Sexp.to_string_hum);
  [%expect
    {|
    (RespList
     ((RespList
       ((BulkString 0-2) (RespList ((BulkString bar) (BulkString baz)))))
      (RespList ((BulkString 1-1) (RespList ((BulkString xx) (BulkString fff)))))))
    |}]

let%expect_test "xrange to id" =
  let result =
    Streams.xrange "-" "0-2" (create_sample_stream ()) |> value_of_result
  in
  Stdio.print_endline (Resp.to_sexp result |> Sexp.to_string_hum);
  [%expect
    {|
    (RespList
     ((RespList
       ((BulkString 0-1) (RespList ((BulkString hello) (BulkString world)))))
      (RespList
       ((BulkString 0-2) (RespList ((BulkString bar) (BulkString baz)))))))
    |}]

let%expect_test "xadd on empty list with listeners" =
  let result =
    Streams.xadd "0-1" [ "hello"; "world" ] 1 (Some (Streams.empty ()))
  in
  Stdio.print_endline
    (Streams.to_sexp (Option.value_exn result.store) |> Sexp.to_string_hum);
  [%expect {| (((id ((millis 0) (sequence 1))) (data ((hello world))))) |}];
  Stdio.print_endline (Resp.to_sexp result.return |> Sexp.to_string_hum);
  [%expect {| (BulkString 0-1) |}];
  List.iter result.notify_with ~f:(fun resp ->
      Stdio.print_endline (Resp.to_sexp resp |> Sexp.to_string_hum));
  [%expect
    {|
    (RespList
     ((RespList
       ((BulkString 0-1) (RespList ((BulkString hello) (BulkString world)))))))
    |}]

let%expect_test "xread existing" =
  let result =
    Streams.xread "key" "0-1" None (create_sample_stream ()) |> value_of_result
  in
  Stdio.print_endline (Resp.to_sexp result |> Sexp.to_string_hum);
  [%expect
    {|
    (RespList
     ((BulkString key)
      (RespList
       ((RespList
         ((BulkString 0-2) (RespList ((BulkString bar) (BulkString baz)))))
        (RespList
         ((BulkString 1-1) (RespList ((BulkString xx) (BulkString fff)))))))))
    |}]

let%expect_test "xread non blocking empty should return NullArray" =
  let result = Streams.xread "key" "0-1" None None |> value_of_result in
  Stdio.print_endline (Resp.to_sexp result |> Sexp.to_string_hum);
  [%expect {| NullArray |}]

let%expect_test "xread blocking should wait forever" =
  let result =
    Streams.xread "key" "0-1" (Some Lifetime.Forever) None |> wait_of_result
  in
  Stdio.print_endline (Lifetime.to_sexp result |> Sexp.to_string_hum);
  [%expect {| Forever |}]
