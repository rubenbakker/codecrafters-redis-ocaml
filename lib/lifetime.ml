open Base

type t = Forever | Expires of int [@@deriving compare, equal, sexp]

let now () : int = Unix.gettimeofday () *. 1000.0 |> Int.of_float

let to_abolute_expires (expires : t) : t =
  match expires with Forever -> Forever | Expires ms -> Expires (now () + ms)

let create_expiry (expiry_type : string) (expiry_value : string) : t =
  let expiry_value = Int.of_string expiry_value in
  let expiry_value =
    match String.lowercase expiry_type with
    | "ex" -> expiry_value * 1000
    | "px" | _ -> expiry_value
  in
  match expiry_value with 0 -> Forever | _ as v -> Expires v

let has_expired (current_time : int) (lifetime : t) : bool =
  match lifetime with Forever -> false | Expires time -> time < current_time

let create_expiry_with_ms (millis : int) : t = Expires millis

let create_expiry_with_s (seconds : float) : t =
  create_expiry_with_ms (Int.of_float Float.O.(seconds * 1000.0))

let to_sexp (value : t) : Sexp.t = sexp_of_t value
