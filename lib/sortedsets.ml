open Base

type entry_t = { value : string; score : float } [@@deriving sexp]
type entries_t = entry_t list [@@deriving sexp]
type t = (string, float, String.comparator_witness) Map.t

let empty () : t = Map.empty (module String)

let add ~(value : string) ~(score : float) (set : t) =
  Map.set set ~key:value ~data:score

let remove ~(value : string) (set : t) = Map.remove set value

let sorted_entries (set : t) : entry_t list =
  Map.to_alist set
  |> List.map ~f:(fun (value, score) -> { value; score })
  |> List.sort ~compare:(fun left right ->
      match Float.compare left.score right.score with
      | 0 -> String.compare left.value right.value
      | result -> result)

let zadd ~(member : string) ~(score : float) (_listener_count : int)
    (set : t option) : t Storeop.mutation_result =
  let set = match set with Some set -> set | None -> empty () in
  let previous_size = Map.length set in
  let result_set = add ~value:member ~score set in
  {
    store = Some result_set;
    return = Resp.Integer (Map.length result_set - previous_size);
    notify_with = [];
  }

let zrem member (_listener_count : int) (set : t option) :
    t Storeop.mutation_result =
  let set = match set with Some set -> set | None -> empty () in
  let previous_size = Map.length set in
  let result_set = Map.remove set member in
  {
    store = Some result_set;
    return = Resp.Integer (previous_size - Map.length result_set);
    notify_with = [];
  }

let zrank ~(value : string) (set : t option) : Storeop.query_result =
  let set = match set with Some set -> set | None -> empty () in
  match
    sorted_entries set
    |> List.map ~f:(fun entry -> (entry.value, entry.score))
    |> List.findi ~f:(fun _ (v, _) -> String.(value = v))
  with
  | Some (idx, _) -> Storeop.Value (Resp.Integer idx)
  | _ -> Storeop.Value Resp.Null

let zcard (set : t option) : Storeop.query_result =
  let set = match set with Some set -> set | None -> empty () in
  Storeop.Value (Resp.Integer (Map.length set))

let zscores (members : string list) (set : t option) : float option list option
    =
  Option.map set ~f:(fun set -> List.map members ~f:(fun m -> Map.find set m))

let zscore_value (member : string) (set : t option) : float option =
  let set = match set with Some set -> set | None -> empty () in
  Map.find set member

let zscore (member : string) (set : t option) : Storeop.query_result =
  let result =
    match zscore_value member set with
    | Some score ->
        Resp.BulkString
          ( Float.to_string score |> fun x ->
            match String.chop_suffix x ~suffix:"." with
            | Some s -> s
            | None -> x )
    | None -> Resp.Null
  in
  Storeop.Value result

let zrange ~(from_idx : int) ~(to_idx : int) (set : t option) :
    Storeop.query_result =
  let set = match set with Some set -> set | None -> empty () in
  let result =
    match Range.normalize (Map.length set) from_idx to_idx with
    | Some (pos, len) ->
        let result =
          sorted_entries set |> List.sub ~pos ~len
          |> List.map ~f:(fun entry -> entry.value)
        in
        Resp.RespList (List.map ~f:(fun value -> Resp.BulkString value) result)
    | None -> Resp.RespList []
  in
  Storeop.Value result
