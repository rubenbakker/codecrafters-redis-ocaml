open Base

type subscriber_t = { thread_id : int; socket : Unix.file_descr }
type channel_t = { name : string; mutable subscribers : subscriber_t list }

let channels : channel_t list ref = ref []
let channels_lock = Stdlib.Mutex.create ()

let find_channel (channel_name : string) : channel_t option =
  List.find ~f:(fun channel -> String.(channel.name = channel_name)) !channels

(** lock the repo for access *)
let unlock () : unit = Stdlib.Mutex.unlock channels_lock

(** unlock the repo after access *)
let lock () : unit = Stdlib.Mutex.lock channels_lock

let protect fn =
  Stdlib.Fun.protect ~finally:unlock (fun () ->
      lock ();
      fn ())

let subscribe (channel_name : string) (thread_id : int)
    (socket : Unix.file_descr) : unit =
  protect (fun _ ->
      match find_channel channel_name with
      | Some channel ->
          channel.subscribers <- { thread_id; socket } :: channel.subscribers
      | None ->
          channels :=
            { name = channel_name; subscribers = [ { thread_id; socket } ] }
            :: !channels)

let publish (channel_name : string) (message : string) : int =
  protect (fun _ ->
      let message_bytes =
        Resp.RespList
          [
            Resp.BulkString "message";
            Resp.BulkString channel_name;
            Resp.BulkString message;
          ]
        |> Resp.to_string |> Bytes.of_string
      in
      match find_channel channel_name with
      | Some channel ->
          channel.subscribers
          |> List.map ~f:(fun subscriber ->
                 Unix.write subscriber.socket message_bytes 0
                   (Bytes.length message_bytes))
          |> List.length
      | None -> 0)

let channels_for_subscriber (thread_id : int) : string list =
  protect (fun _ ->
      List.fold !channels ~init:[] ~f:(fun acc channel ->
          match
            List.find ~f:(fun s -> s.thread_id = thread_id) channel.subscribers
          with
          | Some _ -> channel.name :: acc
          | None -> acc))
