open Base

type subscriber_t = { thread_id : int; socket : Unix.file_descr }
type channel_t = { name : string; mutable subscribers : subscriber_t list }

module Channels = Protected.Resource.Make (struct
  type t = channel_t list

  let state : t ref = ref []
  let get () = !state
  let set c = state := c
end)

let find_channel (channel_name : string) (channels : channel_t list) :
    channel_t option =
  List.find ~f:(fun channel -> String.(channel.name = channel_name)) channels

let subscribe (channel_name : string) (thread_id : int)
    (socket : Unix.file_descr) : unit =
  Channels.mutate (fun channels ->
      match find_channel channel_name channels with
      | Some channel ->
          channel.subscribers <- { thread_id; socket } :: channel.subscribers;
          channels
      | None ->
          { name = channel_name; subscribers = [ { thread_id; socket } ] }
          :: channels)

let unsubscribe (channel_name : string) (thread_id : int) : unit =
  Channels.query (fun channels ->
      match find_channel channel_name channels with
      | Some channel ->
          channel.subscribers <-
            List.filter
              ~f:(fun s -> s.thread_id <> thread_id)
              channel.subscribers
      | None -> ())

let publish (channel_name : string) (message : string) : int =
  Channels.query (fun channels ->
      let message_bytes =
        Resp.RespList
          [
            Resp.BulkString "message";
            Resp.BulkString channel_name;
            Resp.BulkString message;
          ]
        |> Resp.to_string |> Bytes.of_string
      in
      match find_channel channel_name channels with
      | Some channel ->
          channel.subscribers
          |> List.map ~f:(fun subscriber ->
              Unix.write subscriber.socket message_bytes 0
                (Bytes.length message_bytes))
          |> List.length
      | None -> 0)

let channels_for_subscriber (thread_id : int) : string list =
  Channels.query (fun channels ->
      List.fold channels ~init:[] ~f:(fun acc channel ->
          match
            List.find ~f:(fun s -> s.thread_id = thread_id) channel.subscribers
          with
          | Some _ -> channel.name :: acc
          | None -> acc))
