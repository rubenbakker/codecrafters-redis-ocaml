open! Base

type t = string list

let to_resp (existing_list : t) : Resp.t =
  Resp.RespList (List.map ~f:(fun str -> Resp.BulkString str) existing_list)

let llen (existing_list : t option) : Storeop.query_result =
  let result =
    match existing_list with
    | Some l -> Resp.Integer (List.length l)
    | _ -> Resp.Integer 0
  in
  Storeop.Value result

let lrange (from_idx : string) (to_idx : string) (existing_list : t option) :
    Storeop.query_result =
  let from_idx = Int.of_string from_idx in
  let to_idx = Int.of_string to_idx in
  let resp =
    match existing_list with
    | Some l -> (
        match Range.normalize (List.length l) from_idx to_idx with
        | Some (pos, len) ->
            Resp.RespList
              (l |> List.sub ~pos ~len
              |> List.map ~f:(fun str -> Resp.BulkString str))
        | None -> RespList [])
    | _ -> Resp.RespList []
  in
  Storeop.Value resp

let take (existing_list : t) (count : int) : Resp.t list * t * int =
  let count = min count (List.length existing_list) in
  let resps =
    List.take existing_list count |> List.map ~f:(fun s -> Resp.BulkString s)
  in
  ( resps,
    List.sub ~pos:count ~len:(List.length existing_list - count) existing_list,
    List.length existing_list )

let lpush (data : string list) (listener_count : int) (existing_list : t option)
    : t Storeop.mutation_result =
  let values = List.rev data in
  let new_list =
    match existing_list with Some l -> values @ l | _ -> values
  in
  let listener_resp_list, new_list, count = take new_list listener_count in
  {
    store = Some new_list;
    return = Resp.Integer count;
    notify_with = listener_resp_list;
  }

let rpush (data : string list) (listener_count : int) (existing_list : t option)
    : t Storeop.mutation_result =
  let new_list = match existing_list with Some l -> l @ data | _ -> data in
  let listener_resp_list, new_list, count = take new_list listener_count in
  {
    store = Some new_list;
    return = Resp.Integer count;
    notify_with = listener_resp_list;
  }

let lpop (count : int) (_listener_count : int) (existing_list : t option) :
    t Storeop.mutation_result =
  match existing_list with
  | Some existing_list -> (
      match existing_list with
      | first :: rest as l -> (
          match count with
          | 1 ->
              {
                store = Some rest;
                return = Resp.BulkString first;
                notify_with = [];
              }
          | _ ->
              let count = Int.min (List.length l) count in
              let result = List.take l count in
              let new_list =
                List.sub ~pos:count ~len:(List.length l - count) l
              in
              {
                store = Some new_list;
                return =
                  Resp.RespList
                    (List.map ~f:(fun str -> Resp.BulkString str) result);
                notify_with = [];
              })
      | _ -> { store = None; return = Resp.Null; notify_with = [] })
  | _ -> { store = None; return = Resp.Null; notify_with = [] }
