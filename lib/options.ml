open! Base

type role_t = Master | Slave of (string * int)
type rdb_t = { dir : string; filename : string }
type options_t = { port : int; role : role_t; rdb : rdb_t option }

let args_only (argv : string Array.t) : (string * string) list =
  Array.sub ~pos:1 ~len:(Array.length argv - 1) argv
  |> Array.to_list |> Listutils.make_pairs

let value_of_arg (argv : string Array.t) (key : string) : string option =
  List.Assoc.find (args_only argv) ~equal:String.equal key

let get_port (argv : string Array.t) : int =
  match value_of_arg argv "--port" with
  | Some value -> Int.of_string value
  | None -> 6379

let get_role (argv : string Array.t) : role_t =
  match value_of_arg argv "--replicaof" with
  | Some value -> (
      match String.split ~on:' ' value with
      | [ host; port ] -> Slave (host, Int.of_string port)
      | _ -> Master)
  | None -> Master

let get_rdb (argv : string Array.t) : rdb_t option =
  let dir = value_of_arg argv "--dir" in
  let filename = value_of_arg argv "--dbfilename" in
  match (dir, filename) with
  | Some dir, Some filename -> Some { dir; filename }
  | _ -> None

let parse_options (argv : string Array.t) =
  { port = get_port argv; role = get_role argv; rdb = get_rdb argv }
