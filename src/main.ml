open Base
open Unix
open Lib

let empty_context () : Command.context_t =
  let options = Options.parse_options (Sys.get_argv ()) in
  { role = options.role; command_queue = None; post_process = Noop }

let post_process_command (context : Command.context_t)
    (client_socket : file_descr) : Command.context_t =
  ignore
    (match context.post_process with
    | RegisterSlave rdb -> Master.register_slave client_socket rdb
    | Mutation command | Propagate command -> Master.notify_slaves command
    | Noop -> ());
  { context with post_process = Noop }

let rec process_client client_socket (context : Command.context_t) =
  try
    let in_channel = in_channel_of_descr client_socket in
    let out_channel = out_channel_of_descr client_socket in
    let resp_result, _ = Resp.read_from_channel in_channel in
    Stdlib.Printf.printf "process_client: %s\n"
      (Sexp.to_string (Resp.to_sexp resp_result));
    Stdlib.flush Stdlib.stdout;
    let result, context =
      resp_result |> Command.parse_command_line |> Command.process context
    in
    let result = Resp.to_string result in
    Stdlib.Printf.fprintf out_channel "%s" result;
    Stdlib.flush out_channel;
    let context = post_process_command context client_socket in
    process_client client_socket context
  with
  | Unix_error (ECONNRESET, _, _) ->
      Stdlib.print_endline "Error: unix error - reset connection"
  | End_of_file -> Stdlib.print_endline "Error: end of file"
  | _ -> Stdlib.print_endline "Error: Unknown"

let send_to_master ((inch, outch) : Stdlib.in_channel * Stdlib.out_channel)
    (payload : Resp.t) : Resp.t * int =
  let payload = payload |> Resp.to_string in
  Stdlib.Printf.fprintf outch "%s" payload;
  Stdlib.flush outch;
  Resp.read_from_channel inch

let send_to_master_no_answer
    ((_, outch) : Stdlib.in_channel * Stdlib.out_channel) (payload : Resp.t) :
    unit =
  let payload = payload |> Resp.to_string in
  Stdlib.Printf.fprintf outch "%s" payload;
  Stdlib.flush outch

let rec process_slave channels (context : Command.context_t)
    (acc_command_length : int) : unit =
  try
    let inch, _ = channels in
    let command, command_length = Resp.read_from_channel inch in
    Stdlib.Printf.printf "%s -> l=%d -> al=%d\n"
      (Sexp.to_string (Resp.to_sexp command))
      command_length acc_command_length;
    Stdlib.flush Stdlib.stdout;
    ignore
      ((match Command.parse_command_line command with
       | "replconf", [ "GETACK"; "*" ] ->
           let result =
             ("REPLCONF", [ "ACK"; Int.to_string acc_command_length ])
             |> Command.resp_from_command
           in
           send_to_master_no_answer channels result
       | _ ->
           ignore (Command.process context (Command.parse_command_line command)));
       process_slave channels context (acc_command_length + command_length))
  with
  | Unix_error (ECONNRESET, _, _) ->
      Stdlib.print_endline "Error: unix error - reset connection"
  | End_of_file -> Stdlib.print_endline "Error: end of file"
  | _ -> Stdlib.print_endline "Error: Unknown"

let run_and_close_client client_socket =
  let finally () = close client_socket in
  let work () = process_client client_socket (empty_context ()) in
  Stdlib.Fun.protect ~finally work

let rec accept_loop server_socket threads =
  let client_socket, _ = accept server_socket in
  let thread = Thread.create run_and_close_client client_socket in
  accept_loop server_socket (thread :: threads)

let create_command (args : string list) : Resp.t =
  args |> List.map ~f:(fun a -> Resp.BulkString a) |> fun l -> Resp.RespList l

let init_slave (host : string) (port : int) (slave_port : int) =
  let hostaddr =
    try inet_addr_of_string host
    with Failure _ -> (
      try (gethostbyname host).h_addr_list.(0) with _ -> assert false)
  in
  let sock = socket PF_INET SOCK_STREAM 0 in
  connect sock (ADDR_INET (hostaddr, port));
  let in_channel = in_channel_of_descr sock in
  let out_channel = out_channel_of_descr sock in
  let channels = (in_channel, out_channel) in
  ignore (create_command [ "PING" ] |> send_to_master channels);
  ignore
    (create_command [ "REPLCONF"; "listening-port"; Int.to_string slave_port ]
    |> send_to_master channels);
  ignore
    (create_command [ "REPLCONF"; "capa"; "psync2" ] |> send_to_master channels);
  let x, _length =
    create_command [ "PSYNC"; "?"; "-1" ] |> send_to_master channels
  in
  Stdlib.print_endline (Sexp.to_string (Resp.to_sexp x));
  (* rdb file *)
  let x, _ = Resp.read_binary_from_channel in_channel in
  Stdlib.print_endline (Sexp.to_string (Resp.to_sexp x));
  ignore
    (Thread.create (fun () -> process_slave channels (empty_context ()) 0) ())

let () =
  (* Create a TCP server socket *)
  let options = Options.parse_options (Sys.get_argv ()) in
  let server_socket = socket PF_INET SOCK_STREAM 0 in
  setsockopt server_socket SO_REUSEADDR true;
  Stdlib.flush Stdlib.stderr;
  bind server_socket (ADDR_INET (inet_addr_of_string "127.0.0.1", options.port));
  listen server_socket 10;
  ignore (Store.start_gc ());
  ignore (Store.start_expire_listeners ());
  (match options.role with
  | Slave (host, port) -> init_slave host port options.port
  | Master -> ());
  try accept_loop server_socket []
  with e ->
    close server_socket;
    raise e
